{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z#}::Indices(fourD, position=fixed)
{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w,x,y,z#}::Integer(0..4)

{\alpha,\beta,\gamma,\delta,\epsilon,\zeta,\theta,\iota,\kappa,\lambda,\mu,\nu,\rho,\sigma,\tau#}::Indices(threeD, position=fixed, parent=fourD)
{\alpha,\beta,\gamma,\delta,\epsilon,\zeta,\theta,\iota,\kappa,\lambda,\mu,\nu,\rho,\sigma,\tau#}::Integer(1..3)

\delta{#}::KroneckerDelta()
\gamma_{\alpha \beta}::Metric(signature=1)
\gamma^{\alpha \beta}::InverseMetric(signature=1)
\gamma^{\alpha}_{\beta}::KroneckerDelta()
\gamma_{\alpha}^{\beta}::KroneckerDelta()
X^{\alpha \beta}::Symmetric()
X_{\alpha \beta}::Symmetric()
Y^{\alpha \beta}::Symmetric()
Y_{\alpha \beta}::Symmetric()
Z^{\alpha \beta}::Symmetric()
Z_{\alpha \beta}::Symmetric()
\epsilon_{\alpha \beta \gamma}::EpsilonTensor(delta=\delta, metric=\gamma_{\alpha \beta})
\epsilon^{\alpha \beta \gamma}::EpsilonTensor(delta=\delta, metric=\gamma^{\alpha \beta})

\eta^{a? b?}::Symmetric()
\epsilon^{a? b? c? d?}::AntiSymmetric()

{t1,t2,t3,e1,e2,e3,e4,e5,e6,A,B{#},X{#},Y{#},Z{#},U{#},V{#},W{#}}::SortOrder();

ruleM1 := M{1}^{a? b? c? d? e? f? g? h?} ->
            \eta^{a? c?} \eta^{b? d?} \eta^{e? g?} \eta^{f? h?};

ruleM2 := M{2}^{a? b? c? d? e? f? g? h?} ->
            \eta^{a? c?} \eta^{b? e?} \eta^{d? g?} \eta^{f? h?};

ruleM3 := M{3}^{a? b? c? d? e? f? g? h?} ->
            \eta^{a? e?} \eta^{b? f?} \eta^{c? g?} \eta^{d? h?};

ruleM4 := M{4}^{a? b? c? d? e? f? g? h?} ->
            \eta^{a? e?} \eta^{b? g?} \eta^{c? f?} \eta^{d? h?};

ruleM5 := M{5}^{a? b? c? d? e? f? g? h?} ->
            \epsilon^{a? b? c? d?} \eta^{e? g?} \eta^{f? h?};

ruleM6 := M{6}^{a? b? c? d? e? f? g? h?} ->
            \epsilon^{a? b? e? f?} \eta^{c? g?} \eta^{d? h?};

ruleM := M^{a? b? c? d? e? f? g? h?} ->
            e1 * M{1}^{a? b? c? d? e? f? g? h?} +
            e2 * M{2}^{a? b? c? d? e? f? g? h?} +
            e3 * M{3}^{a? b? c? d? e? f? g? h?} +
            e4 * M{4}^{a? b? c? d? e? f? g? h?} +
            e5 * M{5}^{a? b? c? d? e? f? g? h?} +
            e6 * M{6}^{a? b? c? d? e? f? g? h?};

ruleMH1 := M^{a b c d e? f? g? h?} H_{a b c d} ->
            (M^{0 \mu 0 \nu e? f? g? h?} - M^{0 \mu \nu 0 e? f? g? h?}
           - M^{\mu 0 0 \nu e? f? g? h?} + M^{\mu 0 \nu 0 e? f? g? h?}) H1_{\mu \nu} +
            (M^{0 \mu \nu \rho e? f? g? h?} - M^{\mu 0 \nu \rho e? f? g? h?}
           + M^{\nu \rho 0 \mu e? f? g? h?} - M^{\nu \rho \mu 0 e? f? g? h?}) H2_{\mu \nu \rho} +
             M^{\mu \nu \rho \sigma e? f? g? h?} H3_{\mu \nu \rho \sigma};

ruleMH2 := M^{e? f? g? h? a b c d} H_{a b c d} ->
            (M^{e? f? g? h? 0 \mu 0 \nu} - M^{e? f? g? h? 0 \mu \nu 0}
           - M^{e? f? g? h? \mu 0 0 \nu} + M^{e? f? g? h? \mu 0 \nu 0}) H1_{\mu \nu} +
            (M^{e? f? g? h? 0 \mu \nu \rho} - M^{e? f? g? h? \mu 0 \nu \rho}
           + M^{e? f? g? h? \nu \rho 0 \mu} - M^{e? f? g? h? \nu \rho \mu 0}) H2_{\mu \nu \rho} +
             M^{e? f? g? h? \mu \nu \rho \sigma} H3_{\mu \nu \rho \sigma};

ruleH := H_{a b c d} ->
        ( \delta^{0}_{a} \delta^{\mu}_{b} \delta^{0}_{c} \delta^{\nu}_{d}
        - \delta^{0}_{b} \delta^{\mu}_{a} \delta^{0}_{c} \delta^{\nu}_{d}
        - \delta^{0}_{a} \delta^{\mu}_{b} \delta^{0}_{d} \delta^{\nu}_{c}
        + \delta^{0}_{b} \delta^{\mu}_{a} \delta^{0}_{d} \delta^{\nu}_{c} ) *
            H1_{\mu \nu}
     + ( \delta^{0}_{a} \delta^{\mu}_{b} \delta^{\nu}_{c} \delta^{\rho}_{d}
       - \delta^{0}_{b} \delta^{\mu}_{a} \delta^{\nu}_{c} \delta^{\rho}_{d}
       + \delta^{0}_{c} \delta^{\mu}_{d} \delta^{\nu}_{a} \delta^{\rho}_{b}
       - \delta^{0}_{d} \delta^{\mu}_{c} \delta^{\nu}_{a} \delta^{\rho}_{b} ) *
            H2_{\mu \nu \rho}
     + \delta^{\mu}_{a} \delta^{\nu}_{b} \delta^{\rho}_{c} \delta^{\sigma}_{d} *
            H3_{\mu \nu \rho \sigma};

ruleH1 := H1_{\mu \nu} ->
    -2*A*\gamma_{\mu \nu} + X_{\mu \nu};
#ruleH2 := H2_{\mu \nu \rho} ->
#    B_{\nu} \gamma_{\mu \rho} - B_{\rho} \gamma_{\mu \nu}
#    + ((1/2) * \gamma^{\alpha \beta} X_{\alpha \beta} - A) * \epsilon_{\mu \nu \rho}
#    + \epsilon_{\alpha \nu \rho} * Z_{\mu}^{\alpha};
ruleH2 := H2_{\mu \nu \rho} ->
    B_{\nu} \gamma_{\mu \rho} - B_{\rho} \gamma_{\mu \nu}
    + \epsilon_{\alpha \nu \rho} * Z_{\mu}^{\alpha};
#ruleH3 := H3_{\mu \nu \rho \sigma} ->
#    B_{\mu} \epsilon_{\nu \rho \sigma} - B_{\nu} \epsilon_{\mu \rho \sigma}
#    + B_{\rho} \epsilon_{\sigma \mu \nu} - B_{\sigma} \epsilon_{\rho \mu \nu}
#    - (\gamma_{\mu \rho} \gamma_{\nu \sigma} - \gamma_{\mu \sigma} \gamma_{\nu \rho}) *
#        \gamma^{\alpha \beta} X_{\alpha \beta}
#    - \epsilon_{\alpha \mu \nu} \epsilon_{\beta \rho \sigma} Y^{\alpha \beta};
ruleH3 := H3_{\mu \nu \rho \sigma} ->
    - (\gamma_{\mu \rho} \gamma_{\nu \sigma} - \gamma_{\mu \sigma} \gamma_{\nu \rho}) *
        \gamma^{\alpha \beta} X_{\alpha \beta}
    - \epsilon_{\alpha \mu \nu} \epsilon_{\beta \rho \sigma} Y^{\alpha \beta};

ruleEta00 := \eta^{0 0} -> 1;
ruleEta0a1 := \eta^{0 \alpha} -> 0;
ruleEta0a2 := \eta^{\alpha 0} -> 0;
ruleEtaab := \eta^{\alpha \beta} -> -\gamma^{\alpha \beta};

rulegg1 := \gamma^{\alpha \beta} \gamma_{\alpha \beta} -> 3;
rulegg2 := \gamma^{\alpha \beta} \gamma_{\beta \alpha} -> 3;
rulegg3 := \gamma^{\alpha \beta} \gamma_{\beta \gamma} -> \delta^{\alpha}_{\gamma};
rulegg4 := \gamma^{\alpha \beta} \gamma_{\gamma \beta} -> \delta^{\alpha}_{\gamma};
rulegg5 := \gamma^{\beta \alpha} \gamma_{\beta \gamma} -> \delta^{\alpha}_{\gamma};
rulegg6 := \gamma^{\beta \alpha} \gamma_{\gamma \beta} -> \delta^{\alpha}_{\gamma};

ruleTraceFree := Z^{\alpha}_{\alpha} -> 0;

ruleX := X_{\alpha \beta} -> (1/2) * (U_{\alpha \beta} + V_{\alpha \beta});
ruleY := Y_{\alpha \beta} -> (-1/2) * (U_{\alpha \beta} - V_{\alpha \beta});
ruleZ := Z_{\alpha \beta} -> (1/2) * W_{\alpha \beta};

ruleEps0000 := \epsilon^{0 0 0 0} -> 0;
ruleEps000a1 := \epsilon^{0 0 0 \alpha} -> 0;
ruleEps000a2 := \epsilon^{0 0 \alpha 0} -> 0;
ruleEps000a3 := \epsilon^{0 \alpha 0 0} -> 0;
ruleEps000a4 := \epsilon^{\alpha 0 0 0} -> 0;
ruleEps00ab1 := \epsilon^{0 0 \alpha \beta} -> 0;
ruleEps00ab2 := \epsilon^{0 \alpha 0 \beta} -> 0;
ruleEps00ab3 := \epsilon^{0 \alpha \beta 0} -> 0;
ruleEps00ab4 := \epsilon^{\alpha 0 0 \beta} -> 0;
ruleEps00ab5 := \epsilon^{\alpha 0 \beta 0} -> 0;
ruleEps00ab6 := \epsilon^{\alpha \beta 0 0} -> 0;
ruleEps0abc1 := \epsilon^{0 \alpha \beta \gamma} -> -\epsilon^{\alpha \beta \gamma};
ruleEps0abc2 := \epsilon^{\alpha 0 \beta \gamma} -> \epsilon^{\alpha \beta \gamma};
ruleEps0abc3 := \epsilon^{\alpha \beta 0 \gamma} -> -\epsilon^{\alpha \beta \gamma};
ruleEps0abc4 := \epsilon^{\alpha \beta \gamma 0} -> \epsilon^{\alpha \beta \gamma};
ruleEpsabcd := \epsilon^{\alpha \beta \gamma \delta} -> 0;

ex := M^{a b c d e f g h} H_{a b c d} H_{e f g h};
substitute(_, ruleM)
substitute(_, ruleM1)
substitute(_, ruleM2)
substitute(_, ruleM3)
substitute(_, ruleM4)
substitute(_, ruleM5)
substitute(_, ruleM6)
substitute(_, ruleH)
substitute(_, ruleH1)
substitute(_, ruleH2)
substitute(_, ruleH3)
distribute(_, repeat=True)
eliminate_kronecker(_, repeat=True)
substitute(_, ruleEta00)
substitute(_, ruleEta0a1)
substitute(_, ruleEta0a2)
substitute(_, ruleEtaab)
substitute(_, ruleEps0000)
substitute(_, ruleEps000a1)
substitute(_, ruleEps000a2)
substitute(_, ruleEps000a3)
substitute(_, ruleEps000a4)
substitute(_, ruleEps00ab1)
substitute(_, ruleEps00ab2)
substitute(_, ruleEps00ab3)
substitute(_, ruleEps00ab4)
substitute(_, ruleEps00ab5)
substitute(_, ruleEps00ab6)
substitute(_, ruleEps0abc1)
substitute(_, ruleEps0abc2)
substitute(_, ruleEps0abc3)
substitute(_, ruleEps0abc4)
substitute(_, ruleEpsabcd)

eliminate_kronecker(_, repeat=True)
sort_product(_)
canonicalise(_)
rename_dummies(_)
epsilon_to_delta(_)
rewrite_indices(_, $\epsilon_{\alpha \beta \gamma}$, $\gamma^{\alpha\beta}$)
epsilon_to_delta(_)
rewrite_indices(_, $\epsilon_{\alpha \beta \gamma}$, $\gamma^{\alpha\beta}$)
expand_delta(_, repeat=True)
distribute(_, repeat=True)

eliminate_kronecker(_, repeat=True)
eliminate_metric(_, repeat=True)

sort_product(_)
canonicalise(_)
rename_dummies(_)
sort_sum(_)

#rewrite_indices(_, $\epsilon^{\alpha \beta \gamma}$, $\gamma_{\alpha\beta}$)
rewrite_indices(_, $X_{\alpha \beta}$, $\gamma^{\alpha \beta}$)
rewrite_indices(_, $Y^{\alpha \beta}$, $\gamma^{\alpha \beta}$)
rewrite_indices(_, $Z_{\alpha}^{\beta}$, $\gamma^{\alpha \beta}$)
rewrite_indices(_, $Z_{\alpha}^{\beta}$, $\gamma_{\alpha \beta}$)

canonicalise(_)

eliminate_kronecker(_, repeat=True)
eliminate_metric(_, repeat=True)

eliminate_kronecker(_, repeat=True)
eliminate_metric(_, repeat=True)

eliminate_kronecker(_, repeat=True)
eliminate_metric(_, repeat=True)

sort_product(_)
canonicalise(_)
rename_dummies(_)
sort_sum(_)

substitute(_, ruleTraceFree)

canonicalise(_)

rewrite_indices(_, $X_{\alpha \beta}$, $\gamma^{\alpha \beta}$)
rewrite_indices(_, $Y_{\alpha \beta}$, $\gamma^{\alpha \beta}$)
rewrite_indices(_, $Z_{\alpha \beta}$, $\gamma^{\alpha \beta}$)

sort_product(_)
rename_dummies(_)
sort_sum(_)

substitute(_, ruleX)
substitute(_, ruleY)
substitute(_, ruleZ)

distribute(_)

eliminate_kronecker(_, repeat=True)
eliminate_metric(_, repeat=True)

rewrite_indices(_, $\epsilon_{\alpha \beta \gamma}$, $\gamma^{\alpha \beta}$)

sort_product(_)
canonicalise(_)
rename_dummies(_)
sort_sum(_)

substitute(_, $e1 -> t1$)
substitute(_, $e2 -> -6*t1$)
substitute(_, $e3 -> t2$)
substitute(_, $e4 -> 6*t1-2*t2$)
substitute(_, $e5 -> t3$)
substitute(_, $e6 -> -3*t3$)

distribute(_, repeat=True)
sort_product(_)
sort_sum(_)

#factor_in(_, $e1, e2, e3, e4, e5, e6$);
factor_in(_, $t1, t2, t3$);
